
require:
   fs
   path
   "source-map-support" as sm
   "./version" -> version

globals:
   module
   JSON

provide:
   getCache
   install


sm.install{}

getCache{file, opts = {=}} =

   cachedir = path.join{path.dirname{file}, "egcache"}
   cache = path.join{cachedir, path.basename{file}.replace{R".eg$|$", ".js"}}
   cacheoptsFile = cache.replace{R".js$|$", ".json"}

   sstat = fs.statSync{file}
   cstat = fs.statSync{cache} !! e -> null
   cacheopts = JSON.parse{fs.readFileSync{cacheoptsFile, .utf8}} !! e -> null
   newcacheopts = {
      versions = {
         ecmascript = if{opts.es5, 5, 6}
         earlgrey = version
      }
   }

   {g, compiled, srcfile} =
      if not opts.recompile
         \ and cacheopts == newcacheopts
         \ and cstat and sstat.mtime.getTime{} < cstat.mtime.getTime{}:
         if opts.verbose:
            console.error{"Using cached file: " + cache}
         {null, fs.readFileSync{cache, .utf8}, cache}
      else:
         require:
            "./earl-grey" as eg
            "./location" ->
               Source
         if opts.verbose:
            console.error{"Compiling: " + file}
         text = fs.readFileSync{file, .utf8}
         let g = eg.Generator{{sourceMap = true} & opts}
         {=> code, => map} or code and map is null =
            g.generate{Source{text, file}}
         try:
            fs.mkdirSync{cachedir} !! e -> "ignore error"
            fs.writeFileSync{cache, code}
            fs.writeFileSync{cache + ".map", map}
            fs.writeFileSync{cacheoptsFile, JSON.stringify{newcacheopts}}
         catch e:
            console.error{"Failed to cache compiled version of: " + file}
         {g, code, cache}

   {g, compiled, srcfile}


load{opts}{module, file} =
   {_, compiled, srcfile} = getCache{file, opts}
   module._compile{compiled, srcfile}


extensions = {".eg"}

install{opts = {recompile = false, noboil = true, runtime = null}} =
   req = require
   extensions each ext ->
      if not req.extensions[ext]:
         req.extensions[ext] = load{opts}

