
require:
   "./location" ->
      Location, [<<:]

provide:
   tokenize


special_ops = {
   ;; Operators for which the fixity is predetermined regardless
   ;; of whitespace or other factors.
   "(" => .PFX
   "[" => .PFX
   "{" => .PFX
   ")" => .SFX
   "]" => .SFX
   "}" => .SFX
   "," => .IFX
   ":" => .IFX
   "." => .PFX
   "'" => .PFX
   "not" => .PFX
}

regexps = {
   {op3,    op_f}
   {id,     id_f}
   {numr,   numr_f}
   {num,    num_f}
   {str,    str_f}
   {op,     op_f}
   {op2,    op_f}
   {op4,    op_f}
   {indent, indent_f}
   {cmnt,   cmnt_f}
   {unkn,   unkn_f}
} where

  ;; Identifiers
  ;; ex. abc, Abc123, _x, $, a$b$c
  id = R'
     start
     in "a-zA-Z$_"
     * in "a-zA-Z$_0-9"

  id_f = {m} -> #ID{m[0]}


  ;; Numbers with arbitrary radii
  ;; ex. 2r1110, 36rZ, 4r312.33
  numr = R'
     start
     {+d}, in "rR"
     {+ in"A-Za-z0-9_"}
     ?[".", {+ in "A-Za-z0-9_"}]

  numr_f = {m} ->
     {_, radix, intp, frac} = m
     value = parseInt{intp.replace{R.g"_", ""}, radix}
     if frac:
        frac := frac.replace{R.g"_", ""}
        value := value + parseInt{frac, radix} / Math.pow{radix, frac.length}
     #ID{.NUM, value}


  ;; Base 10 numbers
  num = R'
     start
     {+ in "0-9_"}
     ?[".", {+d}]
     ?[in "eE", {? in "+-", + in "0-9_"}]

  num_f = {m} ->
     #NUM{parseFloat{m[0].replace{R.g"_", ""}}}


  ;; Strings (no interpolation)
  str = R'
     start
     "\""
     {*["\\\\" or "\\\"" or [not in "\""]]}
     "\""

  str_f = {m} ->
     repl = {
        "\\\"" => "\""
        "\\\\" => "\\"
        "\\n" => "\n"
     }
     r = m[1].replace{R.g'{"\\\"" or "\\\\" or "\\n"}, {m} -> repl[m]}
     #STR{r}


  ;; Operators
  ;; ex. +, -, **+, ^%, ...
  op = R'
     start
     + in "+\\-*/~^<>=%&|?!@#.:'"

  ;; brackets: [], {}
  op2 = R'
     start
     in "[{}],"

  ;; Word operators: with, each, each?, where%+%, etc.
  op3 = R'
     start
     "with" or "where" or "when"
       \ or "and" or "not" or "or"
       \ or "in" or "mod"
       \ or "each" or "as"
     [+ in "+\\-*/~^<>=%&|?!@#.:'"] or boundary

  ;; `operator`
  op4 = R'
     start
     "`", {+ in "A-Za-z0-9_"}, "`"

  op_f = {m, column} ->
     op = m[1] or m[0]

     match:
        when op === "|" ->
           ;; | is equivalent to an indent to its column
           #INDENT{column - 1}

        when op in special_ops ->
           ;; These operators have predetermined fixities;
           ;; Refer to special_ops before
           fixity = special_ops[op]
           #OP{fixity, op}

        otherwise ->
           #OP{"?FX", op}


  ;; Indent
  indent = R'
     start
     +["\n", {*" "}]

  indent_f = {m} ->
     ilen = m[1].length
     #INDENT{ilen}


  ;; Comment
  cmnt = R'
     start
     ";"
     * not in "\n"

  cmnt_f = {m} -> #IGNORE{}


  ;; Anything else is an error
  unkn = R'
     start
     any

  unkn_f = {m} ->
     #ILLEGAL{m[0]}



;; TODO: ignore blank lines before continuation
ws_re = R'[start, *" ", *["\n", *" ", "\\", *" "]]
eol_re = R'[start, *" ", "\n" or end]


produce{src} =

   text = src.text
   results = {}
   wsb = text.match{ws_re}[0].length
   text := text.slice{wsb}
   pos = wsb
   column = 0

   while text:
      for [i = 0, i < regexps.length, ++i]:
         {re, fn} = regexps[i]
         m = text.match{re}
         if m:
            skip = m[0].length
            endpos = pos + skip
            column :=
               splits = m[0].split{"\n"}
               if [splits.length > 1]:
                  then: splits[splits.length - 1].length
                  else: column + skip
            text := text.slice{skip}
            wsa = text.match{ws_re}[0].length
            eol = text.match{eol_re} and true
            bwsb = wsb > 0
            bwsa = if{eol, bwsb, wsa > 0}
            token = fn{m, column}
            token.wsb := bwsb
            token.wsa := bwsa
            token.location := Location{src, pos, endpos}
            results.push with token
            text := text.slice{wsa}
            column += wsa
            wsb := wsa
            pos := endpos + wsa
            break

   results



indent_tracker{} =
   ;; This returns a function that tracks indentation levels
   ;; as tokens are fed into it. For each token it returns a
   ;; list of tokens including additional commas and brackets
   ;; corresponding to line breaks, indent and dedents.

   curr = false      ;; current indent
   stack = {}        ;; stack of indent levels up to this point
   stacks = {stack}  ;; [ or { start a new stack of indent levels

   ;; We return the following function
   {match token} ->

      #INDENT{match new_indent} ->
         when [curr === false] ->
            ;; This is the first line break, we produce ","
            curr := new_indent
            {#OP{.IFX, ","} & {wsb = true, wsa = true}}

         [> curr] ->
            ;; Indent is larger than before, so we push the previous
            ;; and produce "["
            stack.push{curr}
             curr := new_indent
            {#OP{.PFX, "["} & {wsb = true, wsa = true}}

         [=== curr] ->
            ;; Same as before, ","
            {#OP{.IFX, ","} & {wsb = true, wsa = true}}

         [< curr] ->
            ;; Smaller than before, so we generate "]" until
            ;; the new indent is no larger than the current one
            rval = {}
            while [[stack.length > 0] and [new_indent < curr]]:
               curr := stack.pop{}
               rval.push{#OP{.SFX, "]"} & {wsb = true, wsa = true}}
            rval.push{#OP{.IFX, ","} & {wsb = true, wsa = true}}
            rval

      #ID{*stuff} -> {token}

      #OP{fixity, "[" or "{"} ->
         ;; We restart indent calculation inside each []/{} that
         ;; we find.
         stack.push{curr}
         stacks.push{stack}
         stack := {}
         curr := false
         {token}

      #OP{fixity, "]" or "}"} ->
         ;; We close all indented blocks with every closing bracket
         rval = stack each _ ->
            #OP{.SFX, "]"} & {wsb = true, wsa = true}
         stack := stacks.pop{}
         curr := stack.pop{}
         rval.push{token}
         rval

      other -> {token}

process_indent{stream} =
   tracker = indent_tracker{}
   results = {}
   stream each token ->
      results ++= tracker{token}
   results


disambiguate_fixity{stream} =

   [a <<<: b] =
      a.wsb := b.wsb
      a.wsa := b.wsa
      a <<: b

   collapse_operators{buffer, *match} =
      do: n = buffer.length

      when not buffer.length ->
         {}

      {true?, true?} ->
         match buffer:
            {token and #OP{fixity, name}} ->
               {#ID{name} <<<: token}
            longer ->
               throw E.syntax.nullary{msg, {operators = buffer}} where
                  msg = "Too many consecutive operators were found here."

      {true?, _} ->
         buffer each token and #OP{_, name} ->
            #OP{.PFX, name} <<<: token

      {_, true?} ->
         buffer each token and #OP{_, name} ->
            #OP{.SFX, name} <<<: token

      _ ->
         {first and #OP{fixity, name}, *rest} = buffer
         match {first.wsb, first.wsa}:
            {false?, false?} or {true?, true?} ->
               {#OP{.IFX, name} <<<: first} ++
                  collapse_operators{rest, true, false}
            {true?, _} ->
               {#OP{.PFX, name} <<<: first} ++
                  collapse_operators{rest, true, false}
            {_, true?} ->
               {#OP{.SFX, name} <<<: first} ++
                  collapse_operators{rest, false, false}

   buffer = {}
   pfx = true

   collapse{sfx} =
      rval = collapse_operators{buffer, pfx, sfx}
      buffer := {}
      rval

   results = {}

   stream each match token ->

      #OP{match, name} ->
         "?FX" ->
            buffer.push with token
         .IFX ->
            results ++= collapse{true}
            results.push with token
            pfx := true
         .PFX ->
            results ++= collapse{false}
            results.push with token
            pfx := true
         .SFX ->
            results ++= collapse{true}
            results.push with token
            pfx := false

      other ->
         results ++= collapse{false}
         results.push with token
         pfx := false

   results ++ collapse{true}


alternate_operators{stream} =

   W{x} = if{x, .wide, .short}

   last_op = true
   results = {}

   stream each match token ->

      #IGNORE ->
         null

      #OP{fixity, name} ->
         if last_op:
            results.push with #VOID{}
         match fixity:
            .IFX ->
               results.push with
                  #IFX{W{token.wsa or token.wsb}, name} <<: token
               last_op := true
            .PFX ->
               if [not last_op]:
                  results.push with
                     #IFX{W{token.wsb}, .WHITE}
                     #VOID{}
               results.push with
                  #PFX{W{token.wsa}, name} <<: token
               last_op := true
            .SFX ->
               results.push with
                  #SFX{W{token.wsb}, name} <<: token
                  #VOID{}
               last_op := false

      #ILLEGAL ->
         throw E.syntax.illegal{"unknown character", {chr = token}}

      token ->
         if [not last_op]:
            results.push with
               #IFX{W{token.wsb}, .WHITE}
         results.push{token}
         last_op := false

   if last_op:
      results.push with #VOID{}

   results


fill_locations{source, stream} =
   to_fill = {}
   start = 0
   fill{end} =
      to_fill each
         first token ->
            token.location := Location{source, start, end}
         token ->
            token.location := Location{source, end, end}
      to_fill := {}
   stream each
      {=> location} when location ->
         fill{location.start}
         start := location.end
      other ->
         to_fill.push with other
   fill{source.text.length}
   stream


tokenize{src} =
   chain src:
      produce{@}
      process_indent{@}
      disambiguate_fixity{@}
      alternate_operators{@}
      fill_locations{src, @}

