
require:
   "./earl-grey" as eg
   "./pp" ->
      <>
   "./location" ->
      Source
      format_error
      highlight_locations
      ;; display_error
   fs, path
   "earlgrey-runtime"
   "6to5/polyfill"
   ;; "source-map-support" as smsupport

provide:
   run

globals:
   JSON


;; smsupport.install{}



usage = "Usage: earl [global options] command [options] [arguments...]

earl run [options] [file] [arguments...]
  Run the specified file as a script. The arguments are available in
  process.argv.slice{2} of the script.
  Options:
    -e  --eval <code>  Run the provided code instead of reading from a file.
    -p  --print        Print the result of the evaluation to standard out.
    -i  --interactive  Start an interactive interpreter after execution
                       in the global environment of the script.
    -l  --lazy         Cache previously run scripts

earl compile [options] [file... | dir]
  Compile the specified source files into .js files. The files will be
  placed next to the originals.
  If a directory is given, all .eg files in the directory hierarchy
  will be compiled. The -o option can be used to compile the files
  into a different directory (all non-eg files will be copied over).
  Options:
    -o  --output <dir>  Output in given directory.
    -p  --print         Print the compiled JavaScript to standard out.
                        No files are created.
    -e  --eval <code>   Compile the provided code instead of reading from a file.
    -n  --noboil        Don't output boilerplate code (useful with -p)
    -s  --sourcemaps    Output source maps for the generated files
    -l  --lazy          Only recompile sources that have changed
    -5  --es5           Generate EcmaScript v5
    -6  --es6           Generate EcmaScript v6 (default)
    -v  --verbose       Print information about the operations performed

earl
  Start an interactive session.

Global options:
  -h  --help      Print this message.
  -v  --version   Print the version
"


display_error{match e, rethrow = false} =
   E.syntax? {args => {args}} ->
      console.error{String{e}}
      hls = {.hl1, .hl2, .hl3, .hl4}
      locs = enumerate{items{args}} each
         {i, {key, arg}} when arg and arg.location ->
            {arg.location, hls[i mod 4]}
      var accum = "\n"
      pr{highlight_locations{locs, 0}} where pr{match} =
         String? s ->
            accum += s
         Node? n ->
            {var pre, var post} = match n:
               [.hl1 %]? -> {"\x1B[1;32m", "\x1B[0m"}
               [.hl2 %]? -> {"\x1B[1;33m", "\x1B[0m"}
               [.hl3 %]? -> {"\x1B[1;36m", "\x1B[0m"}
               [.hl4 %]? -> {"\x1B[1;35m", "\x1B[0m"}
               [.lineno %]? ->
                  {"\x1B[1m" + Array{3 - l}.join{" "}
                   ":\x1B[0m "} where
                     l = Math.floor{Math.log{n.children[0]} / Math.log{10}}
               else -> {"", ""}
            if [.sourcepos %]? n:
               pre += " "
            accum += pre
            n.children each child -> pr{child}
            accum += post
            if [div %]? n and [accum[accum.length - 1] != "\n"]:
               accum += "\n"
         Array? a ->
            a each child -> pr{child}
         other ->
            accum += String{other}
      console.error{accum}

   when rethrow ->
      throw e

   else ->
      console.error{e.stack or String{e}}


usage_error{message} =
   console.error{usage}
   console.error{message}
   process.exit{1}


extract_options{clone! argv, opttable, optmessage} =
   results = {=}
   var associate = null

   process_option{original, opt, last} =
      match opttable[opt]:
         undefined? ->
            ;; throw E.option{"Unrecognized option: " + original, original}
            usage_error{"Unrecognized " + optmessage + ": " + original}
         #flag{name} ->
            results[name] = true
         #value{name} when last ->
            associate = name
         #value{name} ->
            results[name] = true

   while argv.length:
      arg = argv.shift{}
      match arg:
         R"^--([a-zA-Z_0-9\-]+)=(.*)"! {_, opt, value} ->
            process_option{arg, opt, true}
            results[associate] = value
            associate = null

         R"^--([a-zA-Z_\-0-9]+)"! {_, opt} ->
            process_option{arg, opt, true}

         R"^-([a-zA-Z_0-9]+)"! {_, opts} ->
            enumerate{opts} each {i, ch} ->
               process_option{arg, ch, i == opts.length - 1}

         other when associate ->
            results[associate] = other
            associate = null

         other ->
            argv.unshift{other}
            break

   {results, argv}





walk{dir, partial, f} =
   stats = fs.statSync{dir} !! e ->
      print "Could not stat: " + dir
      return false

   if stats.isDirectory{}:
      then:
         f{.dir, partial}
         fs.readdirSync{dir} each file ->
            walk{newdir, newpartial, f} where
               newdir = path.join{dir, file}
               newpartial = path.join{partial, file}
      else:
         f{.file, partial}

copy{src, dest} =
   try:
      data = fs.readFileSync{src, .binary}
      fs.writeFileSync{dest, data, .binary}
   catch e:
      print "Could not copy: " + src


_gen{text, file, opts} =
   g = eg.Generator{}
   g.generate{Source{text, path.resolve{file}},
              {noboil = opts.noboil
               to5 = opts.es5 ;; not opts.es6
               sourceMap = opts.sourcemaps}}


interactive{g, e} =
   require: repl
   g.interactive = true
   settings = {
      prompt = "<> "
      ignoreUndefined = true
      async! eval{input, context, filename, callback} =
         try:
            inp = match input:
               R"^\((.*\n)\)$"! {_, x} -> x
               R"^\((.*)\)\n$"! {_, x} -> x
               else -> input
            if inp.trim{} == "":
               callback{null, undefined}
            else:
               var text = g.generateInteractive{Source{inp, "<interactive>"}}
               result = await e{text}
               callback{null, result}
         catch e:
            display_error{e}
            callback{null, undefined}
   }
   runner = repl.start with settings
   runner.on{.exit} with {} -> runner.outputStream.write{"\n"}


compile_path{p, opts} =

   stats = fs.statSync{p}

   match:
      when stats.isDirectory{} ->
         if opts.print:
            usage_error{"Cannot use --print when compiling a directory"}
         out = opts.output or p
         walk{p, "/"} with {*match} ->
            {.dir, d} ->
               from = path.join{p, d}
               if opts.output:
                  to = path.join{out, d}
                  fs.mkdirSync{to} !! e -> "ignore error"
            {.file, match} ->
               R`[start, {*any}, ".eg", end]`! results when results ->
                  {x, name} = results
                  from = path.join{p, x}
                  to = path.join{out, name + ".js"}

                  if opts.lazy:
                     fstat = fs.statSync{from} !! e -> null
                     tstat = fs.statSync{to} !! e -> null
                     if fstat and tstat and fstat.mtime.getTime{} < tstat.mtime.getTime{}:
                        if opts.verbose:
                           print "No change: " + from
                        return true

                  data =
                     try:
                        fs.readFileSync{from, .utf8}
                     catch e:
                        print "Could not read: " + from
                        return false
                  if opts.verbose:
                     console.log{"Compiling:", from}
                  {=> code, => map} = _gen{data, from, opts}
                  try:
                     fs.writeFileSync{to, code + "\n"}
                     if map:
                        fs.writeFileSync{to + ".map", map}
                  catch e:
                     "Could not write: " + to
               when not opts.output ->
                  null
               f ->
                  ;; copy files if output directory is given
                  from = path.join{p, f}
                  to = path.join{out, f}
                  copy{from, to}

      else ->
         data = fs.readFileSync{p, .utf8}
         {=> code, => map} = _gen{data, p, opts}
         match:
            when opts.print ->
               require: "js-beautify" -> js as beaut
               console.log with beaut{code}
            when opts.output ->
               fs.writeFileSync{opts.output, code + "\n"}
               if map:
                  fs.writeFileSync{opts.output + ".map", map}
            else ->
               noext = match p:
                  R"^(.*).eg$"! {_, name} -> name
                  name -> name
               fs.writeFileSync{noext + ".js", code + "\n"}
               if map:
                  fs.writeFileSync{noext + ".js.map", map}


run{argv} =
   {opts, cmd} =
      extract_options{argv, ..., "global option"} with {
         h = #flag{"help"}
         help = #flag{"help"}

         v = #flag{"version"}
         version = #flag{"version"}
      }
   _run{cmd, opts} !!
      e ->
         display_error{e, true}


_run{match, opts} =

   when opts.help ->
      console.log with usage

   when opts.version ->
      console.log with
         "Earl Grey version"
         eg.version

   {} or {.run} ->
      _run{{.run, "-i"}, opts}

   {.run, *rawargs} ->

      {o, rest} = extract_options{rawargs, ..., "option for run"} with {
         e and eval = #value{.evaluate}
         p and .print = #flag{.print}
         i and interactive = #flag{.interactive}
         l and lazy = #flag{.lazy}
      }
      o.noboil = true

      require: "./register" as register
      register.install{o.lazy}

      {file, wd, args} =
         if o.evaluate:
            then:
               {null, process.cwd{}, rest}
            else:
               match rest.shift{}:
                  undefined? when o.interactive ->
                     {null, process.cwd{}, rest}
                  undefined? ->
                     usage_error{"No filename specified."}
                  path.resolve! file ->
                     {file, file, rest}

      var g = null
      var e = null
      var result = null

      if file == null:
         e = eg.evaluator with {
            filename = "<repl>"
            showname = "<repl>"
            cwd = "<repl>"
         }
         g = eg.Generator{o.interactive}
         if o.evaluate:
            result = e{g.generate{Source{o.evaluate, "<repl>"}, o}.code}

      else:
         process.argv = {"earl-run", file} ++ rest

         {g, compiled, srcfile} = register.getCache{file} with {
            interactive = o.interactive
            noboil = o.noboil
            lazy = false
         }
         e = eg.evaluator with {
            filename = file
            showname = srcfile
            cwd = file
         }
         result = e{compiled}

      if o.print:
         print result
      if o.interactive:
         interactive{g, e}


   {.compile, *rawargs} ->

      {o, rest} = extract_options{rawargs, ..., "option for compile"} with {
         o and output     = #value{.output}
         p and "print"    = #flag{.print}
         e and eval       = #value{.evaluate}
         n and noboil     = #flag{.noboil}
         s and sourcemaps = #flag{.sourcemaps}
         l and lazy       = #flag{.lazy}
         "6" and es6      = #flag{.es6}
         "5" and es5      = #flag{.es5}
         v and verbose    = #flag{.verbose}
      }

      require: "./register" as register
      register.install{o.lazy}

      match rest:
         when o.evaluate ->
            {=> code, => map} = _gen{o.evaluate, "<cli>", o}
            match:
               when o.output ->
                  fs.writeFileSync{o.output} with
                     code + "\n"
               else ->
                  require: "js-beautify" -> js as beaut
                  console.log with beaut{code}
         {} ->
            usage_error{"Must provide at least one file or directory to compile."}
         {p} ->
            compile_path{p, o}
         when o.output ->
            usage_error with
               "The -o option cannot be used if more than one file or directory is compiled."
         {*paths} ->
            paths each p ->
               compile_path{p, o}



   stuff ->
      usage_error with
         "Invalid use. Did you mean: earl run " + stuff.join{" "}

