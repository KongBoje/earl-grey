
require:
   "./helpers" -> Body
   "../location" -> [<<:]

inject: mac

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; REQUIRE/PROVIDE AND GLOBALS ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

mac{"__require"}! __require_mac{context, info, form, expr} =
   req = #variable{.require} &: {top = true}
   `[^req][^expr]`


getreqs{info, req, expr} =

   stmts = {}
   names = {}
   pkgs = {}

   topfetch{pkg, v, args = null} =
      pkgs.push with pkg
      stmts.push with
         if args:
            then: `let ^v = [^req]{^pkg}[^args]`
            else: `let ^v = [^req]{^pkg}`
      v

   produce{match expr, fetch} =

      #symbol{s} ->
         names.push with expr
         fetch{#value{s}, expr}

      #value{String? s} ->
         fetch{expr, #symbol{info.gensym{}} &: {reqname = s}}

      #multi{*subp} or #data{*subp} ->
         subp each p -> produce{p, fetch}
         null

      #send{#symbol{"^"}, #data{#void{}, name}} ->
         fetch{name, #symbol{info.gensym{}} &: {reqname = name}}

      #send{#symbol{"as"}, #data{pkg, s}} ->
         names.push with s
         produce{pkg} with {the_pkg, _, args = null} ->
            fetch{the_pkg, s, args}

      #send{#symbol{"->"}, #data{pkg, subp}} ->
         pkgv = produce{pkg, fetch}
         rqn = pkgv.reqname or pkgv[1]
         produce{subp} with {pkg, v, args = null} ->
            stmts.push{`let ^v = ^x`} where x =
               if args:
                  then: `getProperty{^pkgv, ^pkg, ^=rqn}[^args]`
                  else: `getProperty{^pkgv, ^pkg, ^=rqn}`
            v

      #send{name and #symbol{s}, args and #data{*_}} ->
         names.push with name
         fetch{#value{s}, name, args}

   produce{expr, topfetch}
   {stmts, names, pkgs}
   


mac{"require"}! require_mac{context, info, form, match arg} =
   do: req = info.mark{`__require`}

   #void{} ->
      #variable{.require}

   #value{field} ->
      #send{req, arg}

   #data{expr} or expr is arg ->
      {stmts, _, _} = getreqs{info, req, expr}
      #splice{*stmts}


mac{"requireMacros"}! requiremac_mac{context, info, form, match arg} =
   #data{expr} or expr is arg ->
      req = info.mark{`__require`}
      {stmts, vars, pkgs} = getreqs{info, req, expr}
      body =
         #multi{*stmts, #data{*vs}} where
            vs = vars each v -> `= ^v`
      body <<: arg
      the_macros = info.go{body, .parse, .eval}
      #splice{*macs} where macs =
         items{the_macros} each {k, v} when Function? v ->
            var r = #splice{}
            deps = if{v.__deps, clone{v.__deps}, {=}}
            r ++=
               items{v.__deps or {=}} each {name, ename} ->
                  mangled = #variable{info.gensym{name}}
                  deps[name] = mangled
                  `require: ^=[v.__path] -> ^[#symbol{ename}] as ^mangled`
            f{ctx, info, form, expr} =
               info.deps = deps
               v.call{info, expr}
            r.push with
               #declare_raw{#symbol{k} &: {env = info.env}, #macro{f}}
            r


mac{"provide"}! provide_mac{context, _, form, #data{Body! {*expr}}} =
   exp = form.env.mark{`exports`}
   #sink with
      #multi ++
         expr each
            s and #symbol{name} ->
               `[^exp][^=name] = ^s`
            `^s as ^[#symbol{name} or #value{name}]` ->
               `[^exp][^=name] = ^s`
            other ->
               throw E.syntax.provide with
                  "Each clause of provide must be 'sym' or 'sym as name'"

mac{"inject"}! inject_mac{context, _, form, #data{Body! {*expr}}} =
   #restmacro with {stmts} ->
      exp = form.env.mark{`exports`}
      {```
          globals: module
          module.exports{^*expr} =
             var ^exp = {=}
             ^[#multi{*stmts}]
             ^exp
       ```}

mac{"globals"}! globals_mac{context, _, form, #data{Body! {*vars}}} =
   #splice{*globs, `undefined`} where globs =
      vars each variable and #symbol{s} ->
         #declare_raw{variable, #variable{s} &: {top = true}}
